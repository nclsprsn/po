group 'com.capgemini'
version '1.1.0'

apply plugin: 'groovy'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.22'

    compile group: 'ch.qos.logback', name:'logback-classic', version: '1.1.9'
    compile group: 'ch.qos.logback', name:'logback-core', version: '1.1.9'

    compile group: 'org.apache.poi', name: 'poi', version: '3.15'

    compile group:'commons-cli', name: 'commons-cli', version: '1.3'

    compile group: 'com.force.api', name: 'force-partner-api', version: '40.0.0'
    compile group: 'com.force.api', name: 'force-metadata-api', version: '40.0.0'

    compile files('libs/force-tooling-40.1.1.jar')

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'
}

task runScript(type: JavaExec) {
    description 'Run po'

    // Set main property to name of Groovy script class.
    main = 'Po'

    // Set classpath for running the Groovy script.
    classpath = sourceSets.main.runtimeClasspath

    // Set parameters for runnning the Groovy script.
    if(project.hasProperty('args')){
        args project.args.split('\\s+')
    }
}

task buildJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Po',
                   'Implementation-Version': version,
                   'Main-Class': 'Po'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it)  } + 'LICENSE.txt' }
    with jar
}

task buildPackage(type: Zip, dependsOn: buildJar) {
    from "build/libs/${project.name}-${version}.jar"
    from "README.md"
    from "LICENSE.txt"
    from('config') {
        into('config')
    }
    archiveName "${project.name}-${version}.zip"
}

test {
    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
    }
}